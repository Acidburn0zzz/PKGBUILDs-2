# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

_reponame=passwordfile
_pkgname=passwordfile
_android_arch=arm64-v8a
_android_toolchain=aarch64-linux-android

pkgname=android-$_pkgname-$_android_arch
pkgver=3.2.0
pkgrel=1
arch=('any')
pkgdesc="C++ library to read/write passwords from/to encrypted files using AES-256-CBC via OpenSSL (Android, $_android_arch)"
license=('GPL')
depends=("android-c++utilities-$_android_arch" "android-openssl-$_android_arch")
optdepends=("$_pkgname-doc: API documentation")
makedepends=('cmake' 'android-ndk' 'android-sdk')
#checkdepends=('cppunit')
url="https://github.com/Martchus/${_reponame}"
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/Martchus/${_reponame}/archive/v${pkgver}.tar.gz")
sha256sums=('48e95ad852dc83b598c428f9fd0e9717aa12e81aae8254c2946f380afee1b768')
options=(!buildflags staticlibs !strip !emptydirs)

prepare() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
}

build() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
  cmake \
    -DCMAKE_SYSTEM_NAME=Android \
    -DCMAKE_SYSTEM_VERSION=21 \
    -DCMAKE_ANDROID_ARCH_ABI=$_android_arch \
    -DCMAKE_ANDROID_NDK=/opt/android-ndk \
    -DCMAKE_ANDROID_SDK=/opt/android-sdk \
    -DCMAKE_ANDROID_STL_TYPE=gnustl_shared \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/android-libs/$_android_arch \
    -DCMAKE_FIND_ROOT_PATH="/opt/android-ndk/sysroot;/opt/android-libs/$_android_arch" \
    -Dcrypto_DYNAMIC_INCLUDE_DIR="/opt/android-openssl/$_android_arch/include" \
    -Dcrypto_STATIC_INCLUDE_DIR="/opt/android-openssl/$_android_arch/include" \
    -Dcrypto_DYNAMIC_LIB="/opt/android-openssl/$_android_arch/lib/libcrypto.so" \
    -Dcrypto_STATIC_LIB="/opt/android-openssl/$_android_arch/lib/libcrypto.a" \
    -DNO_DOXYGEN=ON
  make VERBOSE=1
}

#check() {
#  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
#  make check
#}

package() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
  make DESTDIR="${pkgdir}" install
}
