# Maintainer: Martchus <martchus@gmx.net>
_name=tageditor
# decide whether to link against the deprecated Qt WebKit module (qt5-webkit package ~40 MB) or
# the newer, chromium-based Qt WebEngine module (qt5-webengine package ~100 MB)
_webviewprovider=qt5-webkit
pkgname=mingw-w64-tageditor
pkgver=1.1.5
pkgrel=7
arch=('any')
pkgdesc="A tageditor with Qt GUI and command line interface supprting MP4 (iTunes), ID3, Vorbis and Matroska."
license=('GPL')
depends=(mingw-w64-{crt,qt5-script,$_webviewprovider,qtutilities,tagparser})
makedepends=('mingw-w64-gcc' 'mingw-w64-qt5-tools')
url="https://github.com/Martchus/tageditor"
source=("tageditor-${pkgver}.tar.gz::https://github.com/Martchus/tageditor/archive/v${pkgver}.tar.gz")
_reponame=tageditor
sha256sums=('SKIP')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    cfg=(mingw-w64-{install,manualstrip-exe,noversion})
    if [[ _webviewprovider == qt5-webkit ]]; then
        cfg+=(forcewebkit)
    fi
    # build utilities for each architecture
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch} && pushd build-${_arch}
        INSTALL_ROOT=$pkgdir/usr/${_arch} ${_arch}-qmake-qt5 ../${_name}.pro $(for val in "${cfg[@]}"; do echo -n "CONFIG+=$val "; done)
        make
        popd
    done
}

package() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    for _arch in ${_architectures}; do
        pushd build-${_arch}
        make install
        popd  
    done
}
