# Maintainer: Martchus <martchus@gmx.net>
_name=tageditor
_reponame=tageditor
# decide whether to link against the deprecated Qt WebKit module (qt5-webkit package ~40 MB) or
# the newer, chromium-based Qt WebEngine module (qt5-webengine package ~100 MB)
_webviewprovider=qt5-webkit
pkgname=mingw-w64-tageditor
pkgver=1.2.1
pkgrel=1
arch=('any')
pkgdesc="A tageditor with Qt GUI and command line interface supporting MP4 (iTunes), ID3, Vorbis and Matroska."
license=('GPL')
depends=(mingw-w64-{crt,qt5-script,$_webviewprovider,qtutilities,tagparser})
makedepends=('mingw-w64-gcc' 'mingw-w64-qt5-tools' 'ffmpeg')
url="https://github.com/Martchus/${_reponame}"
source=("tageditor-${pkgver}.tar.gz::https://github.com/Martchus/${_reponame}/archive/v${pkgver}.tar.gz")
sha256sums=('d089b9e66616c39147b639639152c5848f59dafa2c0a9a77d0137fbed13a2af9')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    cfg=(mingw-w64-{install,manualstrip-exe})
    if [[ _webviewprovider == qt5-webkit ]]; then
        cfg+=(forcewebkit)
    fi
    # build utilities for each architecture
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch} && pushd build-${_arch}
        INSTALL_ROOT=$pkgdir/usr/${_arch} ${_arch}-qmake-qt5 ../${_name}.pro $(for val in "${cfg[@]}"; do echo -n "CONFIG+=$val "; done)
        make -j 1
        popd
    done
}

package() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    for _arch in ${_architectures}; do
        pushd build-${_arch}
        make install
        popd  
    done
}
