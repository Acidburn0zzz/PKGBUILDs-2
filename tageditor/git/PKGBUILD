# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

_name=tageditor
_reponame=tageditor
# decide whether to link against the deprecated Qt WebKit module (in qt5-webkit package ~40 MB) or
# the newer, chromium-based Qt WebEngine module (in qt5-webengine package ~100 MB)
_webviewprovider=qt5-webkit
pkgname=tageditor-git
pkgver=66.77a0bc2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A tag editor with Qt GUI and command-line interface supporting MP4/M4A/AAC (iTunes), ID3, Vorbis, Opus and Matroska (git version)."
license=('GPL')
depends=('qt5-script' $_webviewprovider 'qtutilities' 'tagparser' 'desktop-file-utils' 'xdg-utils')
makedepends=('cmake' 'qt5-tools' 'git')
provides=('tageditor')
conflicts=('tageditor')
install=${_name}.install
url="https://github.com/Martchus/${_reponame}"
source=("${_reponame}::git://github.com/Martchus/${_reponame}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame}"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame}"
  make DESTDIR="${pkgdir}" install
}
