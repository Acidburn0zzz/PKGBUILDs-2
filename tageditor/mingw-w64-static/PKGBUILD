# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

# set the web view provider: either webkit, webengine, auto or none
_webview_provider=none

# set the JavaScript provider: either script, qml, auto or none
_js_provider=qml

# whether the experimental JSON export is enabled: ON or OFF
_json_export=ON

_reponame=tageditor
pkgname=mingw-w64-tageditor-static
_name=${_reponame}
pkgver=2.3.2
pkgrel=1
arch=('any')
pkgdesc='A tag editor with Qt GUI and command-line interface supporting MP4/M4A/AAC (iTunes), ID3, Vorbis, Opus, FLAC and Matroska'
license=('GPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'mingw-w64-cmake' 'mingw-w64-qt5-base-static' 'mingw-w64-qt5-tools' 'mingw-w64-qt5-translations' 'mingw-w64-qt5-svg' 'mingw-w64-qtutilities' 'mingw-w64-tagparser' 'breeze-icons' 'numix-icon-theme-git' 'ffmpeg')
[[ $_webview_provider == webkit ]] && makedepends+=('mingw-w64-qt5-webkit')
[[ $_webview_provider == webengine ]] && makedepends+=('mingw-w64-qt5-webengine')
[[ $_js_provider == script ]] && makedepends+=('mingw-w64-qt5-script')
[[ $_js_provider == qml ]] && makedepends+=('mingw-w64-qt5-declarative')
[[ $_json_export == ON ]] && makedepends+=('mingw-w64-reflective-rapidjson')
conflicts=("${pkgname%-static}")
url="https://github.com/Martchus/${_reponame}"
source=("${_name}-${pkgver}.tar.gz::https://github.com/Martchus/${_reponame}/archive/v${pkgver}.tar.gz")
sha256sums=('d829f4dee076a33d9404c297061dc734848413f2b0cca1a6bdb49ebc253c1864')
options=(!buildflags staticlibs !strip !emptydirs)
_architectures='i686-w64-mingw32 x86_64-w64-mingw32'

build() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX="/usr/${_arch}" \
      -DWEBVIEW_PROVIDER="${_webview_provider}" \
      -DJS_PROVIDER="${_js_provider}" \
      -DENABLE_JSON_EXPORT="${_json_export}" \
      -DREFLECTION_GENERATOR_EXECUTABLE:FILEPATH='/usr/bin/reflective_rapidjson_generator' \
      -DENABLE_STATIC_LIBS:BOOL=ON \
      -DDISABLE_SHARED_LIBS:BOOL=ON \
      -DSTATIC_LINKAGE:BOOL=ON \
      -DBUILTIN_TRANSLATIONS:BOOL=ON \
      -DENABLE_QT_TRANSLATIONS:BOOL=ON \
      -DBUILTIN_ICON_THEMES:STRING='breeze;breeze-dark;Numix' \
      -DIMAGE_FORMAT_SUPPORT:STRING='Gif;ICO;Jpeg' \
      -DSVG_SUPPORT:BOOL=ON \
      -DSVG_ICON_SUPPORT:BOOL=0N \
      ../
    find . -type f -iname 'linklibs.rsp' -exec sed -i "s|-L/lib||g" {} \;
    make
    popd
  done
}

package() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR="${pkgdir}" install-mingw-w64-strip
    popd
  done
}
