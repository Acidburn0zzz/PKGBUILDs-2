# Maintainer: Martchus <martchus@gmx.net>
_name=qtutilities
pkgname=mingw-w64-qtutilities
pkgver=2.0.1
pkgrel=1
arch=('any')
pkgdesc="Common Qt related C++ classes and routines used by my
 applications such as dialogs, widgets and models. (mingw-w64)."
license=('GPL')
depends=('mingw-w64-crt' 'mingw-w64-qt5-base' 'mingw-w64-c++utilities')
makedepends=('mingw-w64-gcc')
url="https://github.com/Martchus/qtutilities"
source=("qtutilities-${pkgver}.tar.gz::https://github.com/Martchus/qtutilities/archive/v${pkgver}.tar.gz")
_reponame=qtutilities
sha256sums=('SKIP')
options=('staticlibs' '!strip')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    # build utilities for each architecture
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch} && pushd build-${_arch}
        INSTALL_ROOT=$pkgdir/usr/${_arch} ${_arch}-qmake-qt5 ../${_name}.pro CONFIG+=mingw-w64-install CONFIG+=mingw-w64-manualstrip-dll CONFIG+=mingw-w64-noversion
        make
        popd
    done
}

package() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    for _arch in ${_architectures}; do       
        pushd build-${_arch}
        make install
        popd             
    done
}
