# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

# set the web view provider: either webkit, webengine, auto or none
_webview_provider=${SYNCTHING_TRAY_WEBVIEW_PROVIDER:-none}

# set the JavaScript provider: either script, qml, auto or none
_js_provider=${SYNCTHING_TRAY_JS_PROVIDER:-qml}

_reponame=syncthingtray
pkgname=mingw-w64-syncthingtray-static
_name=${_reponame}
pkgver=0.8.3
pkgrel=1
arch=('any')
pkgdesc='Tray application for Syncthing (mingw-w64)'
license=('GPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'mingw-w64-cmake' 'mingw-w64-qt5-base-static' 'mingw-w64-qt5-tools' 'mingw-w64-qt5-translations' 'mingw-w64-qt5-svg' 'mingw-w64-qtutilities' 'mingw-w64-openssl' 'breeze-icons' 'numix-icon-theme-git' 'ffmpeg')
[[ $_webview_provider == webkit ]] && makedepends+=('mingw-w64-qt5-webkit')
[[ $_webview_provider == webengine ]] && makedepends+=('mingw-w64-qt5-webengine')
conflicts=("${pkgname%-static}")
url="https://github.com/Martchus/${_reponame}"
source=("${_name}-${pkgver}.tar.gz::https://github.com/Martchus/${_reponame}/archive/v${pkgver}.tar.gz")
sha256sums=('85c7578ce4b2d9c2454cfa4b5b3b5617829d1a77f579e7d33b34bd31542a8b32')
options=(!buildflags staticlibs !strip !emptydirs)
_architectures='i686-w64-mingw32 x86_64-w64-mingw32'

build() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX="/usr/${_arch}" \
      -DWEBVIEW_PROVIDER="${_webview_provider}" \
      -DJS_PROVIDER="${_js_provider}" \
      -DSYSTEMD_SUPPORT=OFF \
      -DNO_FILE_ITEM_ACTION_PLUGIN=ON \
      -DENABLE_STATIC_LIBS:BOOL=ON \
      -DDISABLE_SHARED_LIBS:BOOL=ON \
      -DSTATIC_LINKAGE:BOOL=ON \
      -DBUILTIN_TRANSLATIONS:BOOL=ON \
      -DENABLE_QT_TRANSLATIONS:BOOL=ON \
      -DBUILTIN_ICON_THEMES:STRING='breeze;breeze-dark;Numix' \
      -DIMAGE_FORMAT_SUPPORT:STRING='Gif;ICO;Jpeg' \
      -DSVG_SUPPORT:BOOL=ON \
      -DSVG_ICON_SUPPORT:BOOL=0N \
      ../
    find . -type f -iname 'linklibs.rsp' -exec sed -i "s|-L/lib||g" {} \;
    make
    popd
  done
}

package() {
  cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR="${pkgdir}" install-mingw-w64-strip
    popd
  done
}
