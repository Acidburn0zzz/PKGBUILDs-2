# Maintainer: Martchus <martchus@gmx.net>
# Contributor: ant32 <antreimer@gmail.com>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

# All patches are managed at https://github.com/Martchus/mariadb-connector-c

pkgname=mingw-w64-mariadb-connector-c
pkgver=3.0.2
pkgrel=1
pkgdesc='MariaDB Connector/C is used to connect applications developed in C/C++ to MariaDB and MySQL databases (mingw-w64)'
arch=('any')
url='https://mariadb.com/kb/en/mariadb/about-mariadb-connector-c'
license=('LGPL')
replaces=('mingw-w64-libmariadbclient')
conflicts=('mingw-w64-libmariadbclient')
provides=("mingw-w64-libmariadbclient=$pkgver")
depends=('mingw-w64-crt' 'mingw-w64-openssl' 'mingw-w64-zlib' 'mingw-w64-winpthreads')
makedepends=('mingw-w64-cmake')
options=('!strip' '!buildflags' 'staticlibs')
source=("https://downloads.mariadb.org/interstitial/connector-c-${pkgver}/mariadb-connector-c-${pkgver}-src.tar.gz"
        '0001-Fix-libnames-for-mingw-w64.patch'
        '0002-Fix-_SIZE_T_DEFINED-under-mingw-w64.patch'
        '0003-Use-mingw-w64-winpthreads.patch'
        '0004-Fix-case-of-includes.patch'
        '0005-Don-t-use-program_invocation_short_name-when-compili.patch'
        '0006-Ensure-signal.h-is-included.patch')
sha1sums=('bc1fd6c20610822abdb67fe300bd723fe9225e50'
          '08144bc7e44706c977687660acb7369bfddfeef6'
          'b323d9353aa1779a104ed0970a7afd3236224d4f'
          '061e46028b88ba35a407e52aa7e341fa17c6b3b1'
          '076d3cd8a37c4cbf2a7939139e181bfa687824af'
          '605fa62cb41d5b791f03745b5bbb395574ade8e5'
          '4f80e0a4c344d6e36cfd727a92e55db967c7734c')

_architectures='i686-w64-mingw32 x86_64-w64-mingw32'

prepare() {
  cd "$srcdir/mariadb-connector-c-$pkgver-src/"
  for patch in "$srcdir/"*.patch; do
    patch -p1 -i "$patch"
  done

  # disable auth plugin because it requires SEC_E_INVALID_PARAMETER and other
  # macros which are not provided by mingw-w64
  rm 'plugins/auth/CMakeLists.txt'
}

build() {
  unset LDFLAGS
  cd "$srcdir/mariadb-connector-c-$pkgver-src/"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=RELEASE \
      -DWITH_EXTERNAL_ZLIB=ON \
      -DWITH_SQLITE=OFF \
      -DWITH_OPENSSL=ON \
      -DLIB_EAY=/usr/${_arch}/lib/libcrypto.dll.a \
      -DSSL_EAY=/usr/${_arch}/lib/libssl.dll.a \
      -DWITH_MYSQLCOMPAT=ON \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/mariadb-connector-c-$pkgver-src/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    ln -s mariadb "$pkgdir"/usr/${_arch}/include/mysql
    # Creation of symlinks in CMake project file doesn't work for some reason.
    # Probably a couple more symlinks that should be created.
    ln -s libmariadb.dll.a "$pkgdir"/usr/${_arch}/lib/libmysql.dll.a
    ln -s libmariadbclient.a "$pkgdir"/usr/${_arch}/lib/libmysqlclient.a
    ln -s libmariadb.dll "$pkgdir"/usr/${_arch}/bin/libmysql.dll
    ln -s libmariadb.dll "$pkgdir"/usr/${_arch}/bin/libmysqlclient.dll
    ln -s libmariadb.dll "$pkgdir"/usr/${_arch}/bin/libmysqlclient_r.dll
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/lib/plugin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/plugin/*.a
  done
}
