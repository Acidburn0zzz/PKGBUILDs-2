_name=filezilla
pkgname=mingw-w64-filezilla-svn
pkgver=r7228
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client (mingw-w64, svn version)"
arch=('any')
url="http://filezilla-project.org/"
license=('GPL')
depends=('mingw-w64-crt' 'mingw-w64-hicolor-icon-theme' 'mingw-w64-libpng' 'mingw-w64-dbus' 'mingw-w64-libidn' 'mingw-w64-sqlite' 'mingw-w64-gnutls' 'mingw-w64-libfilezilla-svn')
makedepends=('mingw-w64-gcc' 'mingw-w64-configure' 'mingw-w64-wxmsw' 'wxgtk' 'subversion')
install=
source=("${_name}::svn+https://svn.filezilla-project.org/svn/FileZilla3/trunk")
md5sums=('SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver() {
  cd "${_name}"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "${_name}"
  autoreconf -i
}

build() {
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${_name}/build-${_arch}" && cd "${srcdir}/${_name}/build-${_arch}"
    export PATH="/usr/${_arch}/bin:${PATH}"
    ${_arch}-configure \
      --disable-manualupdatecheck \
      --disable-autoupdatecheck \
      --with-pugixml=builtin
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${_name}/build-${_arch}"
    export PATH="/usr/${_arch}/bin:${PATH}"
    make DESTDIR="${pkgdir}" install
    ${_arch}-strip --strip-all "${pkgdir}"/usr/${_arch}/bin/*.exe
    #${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll
    #${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
    rm -r "${pkgdir}/usr/${_arch}/share/"{icons,man,appdata,applications,pixmaps}
  done
}
