# $Id$
# Maintainer: Martchus <martchus@gmx.net>

pkgname=nginx-mod-rtmp
pkgver=1.2.1
pkgrel=5
_dirname="nginx-rtmp-module-$pkgver"
_nginxver=1.16.0

pkgdesc='Module for nginx that adds RTMP support'
arch=('x86_64')
depends=("nginx=$_nginxver" "libutil-linux")
url='https://github.com/arut/nginx-rtmp-module'
license=('BSD')

source=(
  https://nginx.org/download/nginx-$_nginxver.tar.gz{,.asc}
  "$pkgname-$pkgver::https://github.com/arut/nginx-rtmp-module/archive/v${pkgver}.tar.gz"
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('4fd376bad78797e7f18094a00f0f1088259326436b537eb5af69b01be2ca1345'
            'SKIP'
            '87aa597400b0b5a05274ee2d23d8cb8224e12686227a0abe31d783b3a645ea37')

prepare() {
  cd "$srcdir/$_dirname"
}

build() {
  cd "$srcdir"/nginx-$_nginxver
  ./configure --with-compat --add-dynamic-module="../$_dirname"
  make modules
}

package() {
  install -Dm755 "$srcdir/$_dirname/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  cd "$srcdir"/nginx-$_nginxver/objs
  for mod in ngx_*.so; do
    install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done
}
