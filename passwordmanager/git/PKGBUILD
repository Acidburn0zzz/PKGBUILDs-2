# Maintainer: Martchus <martchus@gmx.net>
_name=passwordmanager
_reponame=passwordmanager
pkgname=passwordmanager-git
pkgver=36.90d37ae
pkgrel=9
arch=('i686' 'x86_64')
pkgdesc="A simple password store using AES-256-CBC encryption via OpenSSL (git version)."
license=('GPL')
depends=('qt5-base' 'qtutilities' 'passwordfile' 'openssl' 'libxkbcommon-x11' 'desktop-file-utils' 'xdg-utils')
makedepends=('qt5-tools' 'git')
provides=('passwordmanager')
conflicts=('passwordmanager')
install=${_name}.install
url="https://github.com/Martchus/${_reponame}"
source=("${pkgname}::git://github.com/Martchus/${_reponame}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${pkgname}"
    INSTALL_ROOT=$pkgdir/usr/ qmake-qt5 "$_name.pro"
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make install
}
