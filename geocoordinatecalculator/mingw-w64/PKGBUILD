# Maintainer: Martchus <martchus@gmx.net>
_name=geocoordinatecalculator
pkgname=mingw-w64-geocoordinatecalculator
pkgver=1.0.1
pkgrel=1
arch=('any')
pkgdesc="Tool for basic calculations with geo coordinates such as format conversions and calculation of distance, bearing, mid point, destination and more (mingw-w64)."
license=('GPL')
depends=('mingw-w64-crt' 'mingw-w64-c++utilities')
makedepends=('mingw-w64-gcc' 'mingw-w64-qt5-base')
url="https://github.com/Martchus/geocoordinatecalculator"
source=("geocoordinatecalculator-${pkgver}.tar.gz::https://github.com/Martchus/geocoordinatecalculator/archive/v${pkgver}.tar.gz")
_reponame=geocoordinatecalculator
sha256sums=('SKIP')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    # build for each architecture
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch} && pushd build-${_arch}
        INSTALL_ROOT=$pkgdir/usr/${_arch} ${_arch}-qmake-qt5 ../${_name}.pro CONFIG+=mingw-w64-install CONFIG+=mingw-w64-manualstrip-exe CONFIG+=mingw-w64-noversion
        make
        popd
    done
}

package() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    for _arch in ${_architectures}; do       
        pushd build-${_arch}
        make install
        popd
    done
}
