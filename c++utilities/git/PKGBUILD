# Maintainer: Martchus <martchus@gmx.net>
_name=c++utilities
_reponame=cpp-utilities
pkgname=c++utilities-git
pkgver=50.4d6bf67
pkgrel=5
arch=('i686' 'x86_64')
pkgdesc="Common C++ classes and routines used by my applications such
as argument parser, IO and conversion utilities (git version)."
license=('GPL')
depends=('gcc-libs')
makedepends=('qt5-base' 'git')
provides=('c++utilities')
conflicts=('c++utilities')
url="https://github.com/Martchus/${_reponame}"
source=("${pkgname}::git://github.com/Martchus/${_reponame}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${pkgname}"
    INSTALL_ROOT=$pkgdir/usr/ qmake-qt5 "$_name.pro"
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make install
}
