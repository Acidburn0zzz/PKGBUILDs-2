# Maintainer: Martchus <martchus@gmx.net>
_name=c++utilities
pkgname=mingw-w64-c++utilities
pkgver=2.0.1
pkgrel=1
arch=('any')
pkgdesc="Common C++ classes and routines used by my applications such
as argument parser, IO and conversion utilities (mingw-w64)."
license=('GPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'mingw-w64-qt5-base')
url="https://github.com/Martchus/cpp-utilities"
source=("c++utilities-${pkgver}.tar.gz::https://github.com/Martchus/cpp-utilities/archive/v${pkgver}.tar.gz")
_reponame=cpp-utilities
sha256sums=('13cd2a9fb394d0da7656880adb1e358fc40f1b4860f77fd64970c4d00f70b05f')
options=('staticlibs' '!strip')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    # build for each architecture
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch} && pushd build-${_arch}
        INSTALL_ROOT=$pkgdir/usr/${_arch} ${_arch}-qmake-qt5 ../${_name}.pro CONFIG+=mingw-w64-install CONFIG+=mingw-w64-manualstrip-dll CONFIG+=mingw-w64-noversion
        make
        popd
    done
}

package() {
    cd "$srcdir/${PROJECT_DIR_NAME:-$_reponame-$pkgver}"
    for _arch in ${_architectures}; do
        pushd build-${_arch}
        make install
        popd
    done
}
