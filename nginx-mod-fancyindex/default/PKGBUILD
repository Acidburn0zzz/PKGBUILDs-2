# $Id$
# Maintainer: Martchus <martchus@gmx.net>

pkgname=nginx-mod-fancyindex
pkgver=0.4.3
pkgrel=5
_dirname="ngx-fancyindex-${pkgver}"
_nginxver=1.16.1

pkgdesc='Fancy indexes module for the Nginx web server'
arch=('x86_64')
depends=("nginx=$_nginxver" "libutil-linux")
url='https://github.com/aperezdc/ngx-fancyindex'
license=('CUSTOM')

source=(
  https://nginx.org/download/nginx-$_nginxver.tar.gz{,.asc}
  "$pkgname-$pkgver::https://github.com/aperezdc/ngx-fancyindex/archive/v${pkgver}.tar.gz"
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('4fd376bad78797e7f18094a00f0f1088259326436b537eb5af69b01be2ca1345'
            'SKIP'
            '81698fb0c1ec9f906ce308c055d5d248085caf390f4b92516c1ec93f87c886d4')

prepare() {
  cd "$srcdir/$_dirname"
}

build() {
  cd "$srcdir"/nginx-$_nginxver
  ./configure --with-compat --add-dynamic-module="../$_dirname"
  make modules
}

package() {
  install -Dm755 "$srcdir/$_dirname/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  cd "$srcdir"/nginx-$_nginxver/objs
  for mod in ngx_*.so; do
    install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done
}
